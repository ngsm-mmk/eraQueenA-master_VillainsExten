@NINGAI_SHOP
REPEAT 1000
	TFLAG:COUNT = ITEMSALES:COUNT
	ITEMSALES:COUNT = 0
REND

CALL CHARA_LIST_NINGAI

ITEMSALES:0 = 0

CALL CHARA_SUMMON_SHOW

$INPUT_LOOP
INPUT
IF RESULT == 1
	IF TFLAG:100 > 0
		TFLAG:100 -= 1
		CALL CHARA_LIST_NINGAI
	ENDIF
	;バグ对策。キャラ上限を超えるとITEMSALES:0が登录されるので
	ITEMSALES:0 = 0

	CALL CHARA_SUMMON_SHOW
	GOTO INPUT_LOOP
ELSEIF RESULT == 9
	;最大ページ数と比較、ページ数変動の際は
	IF TFLAG:100 < TFLAG:102
		TFLAG:100 += 1
		CALL CHARA_LIST_NINGAI
	ENDIF
	;バグ对策。キャラ上限を超えるとITEMSALES:0が登录されるので
	ITEMSALES:0 = 0

	CALL CHARA_SUMMON_SHOW
	GOTO INPUT_LOOP
;不买了なら返回
ELSEIF RESULT == 0
	CALL CHARA_SUMMOM_AFTER
	RETURN 0
;人外番号417～1000
ELSEIF RESULT < 417 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF

;C = 角色番号
B = RESULT
D = RESULT + 2000
C = RESULT
;購入済みor購入不可＝フラグ値が0なら再输入
SIF FLAG:D == 0 || ITEMSALES:RESULT == 0
	GOTO INPUT_LOOP
;キャラ解説を入れる
FLAG:9 = C
CALL CHARA_MANUAL

;不是を選ぶと再输入
IF FLAG:9 == 0
	CALL CHARA_SUMMON_SHOW
	GOTO INPUT_LOOP
ELSE
	FLAG:9 = 0
ENDIF
;フラグ値＝売値が所持金より多いなら再输入
IF MONEY < FLAG:D
	PRINTW 余额不足

	CALL CHARA_SUMMON_SHOW
	GOTO INPUT_LOOP
ENDIF
;上記処理を抜けてきた＝購入可能で所持金が売値以上
MONEY -= FLAG:D
FLAG:D = 0
ITEM:B = 1
ADDCHARA C
TARGET = CHARANUM-1


CFLAG:111 = -1
CFLAG:1000 = -1
CFLAG:1001 = -1
;相性の初期値
REPEAT CHARANUM
	RELATION:(NO:COUNT) = 100
	RELATION:COUNT:(NO:TARGET) = 100
REND

CALL CHARA_SUMMOM_AFTER
RETURN 0

@CHARA_LIST_NINGAI
REPEAT 1000
    SIF COUNT <= 416
        CONTINUE
	A = COUNT
	ITEMSALES:A = 0
REND

CALL CHARA_SUMMON_PRICE

F = 0
TFLAG:101 = 0
REPEAT 1000
    SIF COUNT <= 416
        CONTINUE
	E = COUNT + 2000
	I = COUNT
	IF FLAG:E > 0
		TFLAG:101 += 1
		SIF (TFLAG:101 <= TFLAG:100 * 30) || F >= 30
			CONTINUE
		ITEMSALES:I = 1
		F += 1
	ENDIF
REND

TFLAG:102 = TFLAG:101 / 30

@CHARA_SUMMON_PRICE
CALL CHARA_PRICE

REPEAT 1000
    SIF COUNT <= 416
        CONTINUE
	I = COUNT
	D = COUNT + 2000
	SIF ITEM:I != 0
		FLAG:D = 0
REND

@CHARA_SUMMOM_AFTER
REPEAT 1000
    SIF COUNT <= 416
        CONTINUE
	A = COUNT
	ITEMSALES:A = 0
	ITEMSALES:COUNT = TFLAG:COUNT
	TFLAG:COUNT = 0
REND

@CHARA_SUMMON_SHOW
REPEAT CHARANUM
    SIF NO:COUNT == 200
        X = COUNT
REND
CALL KAIGYO
DRAWLINE
PRINTFORML %CALLNAME:X%：
PRINTFORM 「那么，请选择已拥有的契约书，以相应金额激活」
PRINTFORML  所持金：${MONEY} 　＜page{TFLAG:100 + 1}/{TFLAG:102 + 1}＞
DRAWLINE
PRINT_SHOPITEM

DRAWLINE
IF TFLAG:100 > 0
	PRINTC [1]上一页
ELSE
	PRINTC 　
ENDIF
PRINTC [0]不买了
SIF TFLAG:100 < TFLAG:102
	PRINTC [9]下一页
PRINTL