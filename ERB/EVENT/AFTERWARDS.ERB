;=============================================================================
;拍卖後エピソード関連の関数
;=============================================================================
;エンディング時に拍卖したキャラのエピソードを流す
;エンディング後に呼ばれる。戻った後にゲーム終了ＯＲ強くてニューゲーム
;-------------------------------------------------
;エンディング後エピソードを表示させるか選択
;-------------------------------------------------
@SHOW_AFTERWARDS
;FLAG:500 = エピソードを显示奴隶数
;一人もエピソードがないなら何もせず終了
SIF FLAG:500 == 0
	RETURN 0

;選択肢
PRINTFORM %PLAYER_NAME(1)%调教的奴隶
SIF FLAG:500 > 1
	PRINT 们
PRINTL 被拍卖后的后续……
PRINTC [0]看
PRINTC [1]不看
$INPUT_LOOP
INPUT
IF RESULT == 1
	GOTO AFTERWARDS_RESET
ELSEIF RESULT != 0
	GOTO INPUT_LOOP
ENDIF

;記録できる人数を超えた場合、古いものから順に显示ようにしたかったらしい
A = FLAG:500
IF A > 10
	A = 10
	W:1 = FLAG:500 % 10
ELSE
	W:1 = 0
ENDIF

REPEAT A
	W = 501 + (COUNT + W:1) % 10
	X = FLAG:W % 1000
	Z = FLAG:W / 1000
	CALL SHOW_SLAVE_AFTERWARDS
REND

$AFTERWARDS_RESET
;エピソードフラグ消去
W = 501
REPEAT 10
	FLAG:W = 0
	W += 1
REND
;エピソードを显示奴隶数重置
FLAG:500 = 0
RETURN 0

;-------------------------------------------------
;拍卖時の処理
;-------------------------------------------------
@SET_AFTERWARDS
;拍卖キャラエピソードをセット
X = CFLAG:TARGET:600
Z = 0
CALL SET_SLAVE_AFTERWARDS
;不显示場合は記録しない
SIF Z == 0
	RETURN 0
;%10+501の意味：エピソード関連で使用する変数がFLAG:501から10人分だから
Y:0 = FLAG:500 % 10 + 501
Y:1 = Z * 1000 + NO:TARGET
FLAG:Y = Y:1
;エピソードを显示奴隶数をカウント
FLAG:500 += 1

RETURN 0
