;=================================================
;   SYSTEM:读取処理関数
;-------------------------------------------------
@SYSTEM_LOADGAME
;-------------------------------------------------
#DIM L_LEN
#DIM L_POS
#DIM L_IDX
#DIM L_LINECOUNT

L_POS = L_POS < 0 ? 0 # L_POS
L_LEN = SAVENOS()
L_LINECOUNT = LINECOUNT

$DRAW_PAGE
REDRAW 0
DRAWLINE
PRINTL 要载入哪个存档？
DRAWLINE

CALL SYSTEM_LIST_DATA, L_POS, L_POS + L_LEN

CHKDATA 99
IF RESULT == 0
	DRAWLINE
	SIF 99 == LASTLOAD_NO
		SETCOLORBYNAME DEEPSKYBLUE
	PRINTFORML  [{99,2}] %RESULTS%
	RESETCOLOR
	DRAWLINE
ELSE
	DRAWLINE
ENDIF

PRINTFORMLC [101] 上一页
PRINTFORMLC [100] 返回
PRINTFORMLC [102] 下一页
PRINTL

REDRAW 1
$INPUT_LOOP
INPUT 99

L_IDX = RESULT
SELECTCASE L_IDX
;[100] 返回
CASE 100
	RETURN L_POS

;[101] 上一页
CASE 101
	IF L_POS - L_LEN >= 0
		L_POS -= L_LEN
		CLEARLINE LINECOUNT - L_LINECOUNT
		GOTO DRAW_PAGE
	ENDIF

;[102] 下一页
CASE 102
	IF L_POS + L_LEN < 99
		L_POS += L_LEN
		CLEARLINE LINECOUNT - L_LINECOUNT
		GOTO DRAW_PAGE
	ENDIF

;- 範囲内
CASE 0 TO 99
	CHKDATA L_IDX
	IF RESULT == 0
		REDRAW 1
		;- 读取(実行後、@EVENTLOADへ遷移)
		LOADDATA L_IDX
		RETURN L_POS
	ENDIF
ENDSELECT

;无效输入
CLEARLINE 1
GOTO INPUT_LOOP


;=================================================
;   SYSTEM:保存処理関数
;-------------------------------------------------
@SYSTEM_SAVEGAME
;-------------------------------------------------
#DIM L_LEN
#DIM L_POS
#DIM L_IDX
#DIM L_LINECOUNT

L_POS = L_POS < 0 ? 0 # L_POS
L_LEN = SAVENOS()
L_LINECOUNT = LINECOUNT

$DRAW_PAGE
REDRAW 0
DRAWLINE
PRINTL 要写入到哪个存档？
DRAWLINE

CALL SYSTEM_LIST_DATA, L_POS, L_POS + L_LEN

DRAWLINE

PRINTFORMLC [101] 上一页
PRINTFORMLC [100] 返回
PRINTFORMLC [102] 下一页               [300] 删除存档


REDRAW 1
$INPUT_LOOP
INPUT

L_IDX = RESULT
SELECTCASE L_IDX
;[100] 返回
CASE 100
	RETURN L_POS

;[101] 上一页
CASE 101
	IF L_POS - L_LEN >= 0
		L_POS -= L_LEN
		CLEARLINE LINECOUNT - L_LINECOUNT
		GOTO DRAW_PAGE
	ENDIF

;[102] 下一页
CASE 102
	IF L_POS + L_LEN < 99
		L_POS += L_LEN
		CLEARLINE LINECOUNT - L_LINECOUNT
		GOTO DRAW_PAGE
	ENDIF

;[300] 删除存档
CASE 300
	CLEARLINE LINECOUNT - L_LINECOUNT
	CALL SYSTEM_DELDATA, L_POS
	L_POS = RESULT
	GOTO DRAW_PAGE

;- 範囲内
CASE 0 TO 98
	CHKDATA L_IDX
	IF RESULT == 0
		PRINTL 存档已经存在，确定要覆盖么？
		PRINTL [1] 确定    [0] 取消
		INPUT 0
		IF RESULT != 1
			CLEARLINE LINECOUNT - L_LINECOUNT
			GOTO DRAW_PAGE
		ENDIF
	ENDIF
	GOTO SAVE_GAME
ENDSELECT

;无效输入
CLEARLINE 1
GOTO INPUT_LOOP


$SAVE_GAME
SAVEDATA_TEXT =
CALL SAVEINFO
LOCALS = %GETTIMES()% %SAVEDATA_TEXT%
SAVEDATA L_IDX, LOCALS
SAVEGLOBAL

PRINTFORMW 已将游戏保存为{L_IDX}号存档……
RETURN L_POS


; CLEARLINE LINECOUNT - L_LINECOUNT
GOTO DRAW_PAGE



;=================================================
;   SYSTEM:删除存档処理関数
;-------------------------------------------------
@SYSTEM_DELDATA, L_POS = -1
;-------------------------------------------------
#DIM L_LEN
#DIM L_POS
#DIM L_IDX
#DIM L_LINECOUNT

L_POS = L_POS < 0 ? 0 # L_POS
L_LEN = SAVENOS()
L_LINECOUNT = LINECOUNT

$DRAW_PAGE
REDRAW 0
DRAWLINE
PRINTL 要删除哪个存档？
DRAWLINE

CALL SYSTEM_LIST_DATA, L_POS, L_POS + L_LEN

DRAWLINE

PRINTFORMLC [101] 上一页
PRINTFORMLC [100] 返回
PRINTFORMLC [102] 下一页
PRINTL

REDRAW 1
$INPUT_LOOP
INPUT 99

L_IDX = RESULT
SELECTCASE L_IDX
;[100] 返回
CASE 100
	RETURN L_POS

;[101] 上一页
CASE 101
	IF L_POS - L_LEN >= 0
		L_POS -= L_LEN
		CLEARLINE LINECOUNT - L_LINECOUNT
		GOTO DRAW_PAGE
	ENDIF

;[102] 下一页
CASE 102
	IF L_POS + L_LEN < 99
		L_POS += L_LEN
		CLEARLINE LINECOUNT - L_LINECOUNT
		GOTO DRAW_PAGE
	ENDIF

;- 範囲内
CASE 0 TO 99
	CHKDATA L_IDX
	IF RESULT == 0
		PRINTL 确定要删除这个存档么？
		PRINTL [1] 确定    [0] 取消
		INPUT 0
		IF RESULT != 1
			CLEARLINE LINECOUNT - L_LINECOUNT
			GOTO DRAW_PAGE
		ELSE
			DELDATA L_IDX
			GOTO DRAW_PAGE
		ENDIF
	ENDIF
ENDSELECT

;无效输入
CLEARLINE 1
GOTO INPUT_LOOP

;=================================================
;   SYSTEM:显示存档列表
;-------------------------------------------------
@SYSTEM_LIST_DATA, ARG, ARG:1
;-------------------------------------------------
#DIM L_I

FOR L_I, ARG, ARG:1
	IF L_I >= 99
		PRINTL
		CONTINUE
	ENDIF

	CHKDATA L_I
	IF RESULT == 0
		PRINTFORML  [{L_I,2}] %RESULTS%
	ELSE
		SETCOLORBYNAME GRAY
		PRINTFORML  [{L_I,2}] ----
		RESETCOLOR
	ENDIF
NEXT