;FileName_SYSTEM_DEBUG_Universal.ERB -------------------------------- Ver1.00
;启动调试模式: @DEBUG_MENU_U
;使用例子:
;ELSEIF RESULT == 9988
;	CALL DEBUG_MENU_U
;原作者：Tsubasa (2015/5/28 4th Version)
;源文件: SYSTEM_DEBUG_Universal_eraTW.ERB
;魔改:star
;=============================================================================
;用于显示角色列表的命令安装群
;=============================================================================
;-------------------------------------------------
;用于设置列表角色数据的函数
;-------------------------------------------------
;CALLF SET_LIST, [リスト位置], [キャラ番号]
@SET_LIST(ARG, ARG:1)
#FUNCTION
RETURNF LIST_DATA("SET", ARG, ARG:1)

;-------------------------------------------------
;用于设置列表角色数据的函数
;-------------------------------------------------
;[変数等] = GET_LIST([リスト位置])
@GET_LIST(ARG)
#FUNCTION
RETURNF LIST_DATA("GET", ARG)

;-------------------------------------------------
;内部使用的函数
;-------------------------------------------------
;直接これを呼び出す必要はない
@LIST_DATA(ARGS, ARG, ARG:1)
#FUNCTION
IF ARGS == "SET"
	LOCAL:ARG = ARG:1
	RETURNF ARG:1
ELSEIF ARGS == "GET"
	RETURNF LOCAL:ARG
ELSEIF ARGS == "CLEAR"
	VARSET LOCAL, 0
	RETURNF -1
ELSE
	RETURNF -1
ENDIF

;-------------------------------------------------
;清除列表的函数
;-------------------------------------------------
;CALLF CLEAR_LIST
@CLEAR_LIST
#FUNCTION
RETURNF LIST_DATA("CLEAR")

;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;用于调试模式的特殊指令
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;以下焼き芋弁当パッチ用のデバッグモードを起動しているときに使える特殊な処理をここで一括で管理
;大きく分けて調教中に使えるものとメイン菜單(SHOP)が開いているときに使用できるものに分かれる
;=============================================================================
;調教中に使用可能な指令
;=============================================================================
@DEBUG_MENU_U
DRAWLINE
PRINTFORML 已经第{DAY}天
TARGET = 0
IF TARGET >= 0
	PRINTFORM %CALLNAME% 选择中　
ENDIF
PRINTFORML (现有${MONEY})
DRAWLINE
PRINTL 你要准备做什么？
PRINTL 
PRINTLCD [ 0] - 角色编辑
PRINTLCD [ 1] - 修改金钱
PRINTLCD [ 2] - FLAG操作
PRINTLCD [ 3] - 角色全回复
PRINTL
PRINTLCD [ 4] - ITEM编辑
PRINTLCD [ 5] - TFLAG编辑
PRINTLCD [99] - 跳至下一天
PRINTLCD [100] - 返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 0
	LOCAL = MASTER
	LOCAL:1 = TARGET
	LOCAL:2 = ASSI
	CALL EDIT_CHARA
	MASTER = LOCAL
	TARGET = LOCAL:1
	ASSI = LOCAL:2
ELSEIF RESULT == 1
	CALL EDIT_MONEY
ELSEIF RESULT == 2
	CALL EDIT_FLAG
ELSEIF RESULT == 3
	CALL EDIT_HEAL_ALL
ELSEIF RESULT == 4
	CALL EDIT_ITEM
ELSEIF RESULT == 5
	CALL EDIT_TFLAG
ELSEIF RESULT == 99
	CALL NEXTDAY
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART

;--------------------------------------------------
;所持金変更処理
;--------------------------------------------------
@EDIT_MONEY
PRINTFORML (持有${MONEY:0} or 持有${MONEY:1} or 持有${MONEY:2})
DRAWLINE
FOR LOCAL, 0, 3
	PRINTFORML [{LOCAL, 3}]MONEY:{LOCAL, 3}  = {MONEY:LOCAL}
NEXT
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 3
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML MONEY:{LOCAL} 修改为：(现在{MONEY:LOCAL})
	PRINTFORML 请输入数值[0-99999999]（输入无效数值则取消）
	INPUT
	SIF RESULT < 0 || RESULT > 99999999
		RESTART
	MONEY:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
;全角色全回复
;--------------------------------------------------
@EDIT_HEAL_ALL
REPEAT CHARANUM
	BASE:COUNT:0 = MAXBASE:COUNT:0
	BASE:COUNT:1 = MAXBASE:COUNT:1
REND
PRINTW 全员体力与气力已回复

;--------------------------------------------------
;キャラ書き換え
;--------------------------------------------------
@EDIT_CHARA
;前后のキャラを表示できるか判定
LOCAL:1 = -1
LOCAL:2 = -1
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == TARGET
		CONTINUE
	IF LOCAL < TARGET
		LOCAL:1 = LOCAL
	ELSE
		LOCAL:2 = LOCAL
		BREAK
	ENDIF
NEXT

DRAWLINE
PRINTFORML \@(TARGET < 0) ? 请选择对象 # 现在「%CALLNAME%」选择中\@
DRAWLINE
PRINTFORMLC [  0] - \@(TARGET >= 0) ? 对象变更 # 对象选择\@
IF TARGET >= 0
	;PRINTLC [  1] - 对象情报
	PRINTLC [  2] - ABL编辑
	PRINTLC [  3] - EXP编辑
	PRINTLC [  4] - MARK编辑
	PRINTL
	PRINTLC [  5] - TALENT编辑
	PRINTLC [  6] - JUEL编辑
	PRINTLC [  7] - CFLAG操作
	PRINTLC [  8] - PALAM编辑
	PRINTL
	PRINTLC [  9] - BASE编辑
	PRINTLC [ 10] - MAXBASE编辑
ENDIF
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL:1 < 0) ? %" " * 16% # [1011]%CALLNAME:(LOCAL:1)% 编辑\@
PRINTFORMLC [1000]返回
PRINTFORMLC \@(LOCAL:2 < 0) ? %" " * 16% # [1019]%CALLNAME:(LOCAL:2)% 编辑\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1011 && LOCAL:1 >= 0
	TARGET = LOCAL:1
ELSEIF RESULT == 1019 && LOCAL:2 >= 0
	TARGET = LOCAL:2
ELSEIF RESULT == 0
	CALL SELECT_TARGET
ELSEIF TARGET < 0
	PRINTW 对象已编辑。
;ELSEIF RESULT == 1
	;CALL SINGLE_CHARA_STATE(TARGET,1)
ELSEIF RESULT == 2
	CALL EDIT_ABL
ELSEIF RESULT == 3
	CALL EDIT_EXP
ELSEIF RESULT == 4
	CALL EDIT_MARK
ELSEIF RESULT == 5
	CALL EDIT_TALENT
ELSEIF RESULT == 6
	CALL EDIT_JUEL
ELSEIF RESULT == 7
	CALL EDIT_CFLAG
ELSEIF RESULT == 8
	CALL EDIT_PALAM
ELSEIF RESULT == 9
	CALL EDIT_BASE
ELSEIF RESULT == 10
	CALL EDIT_MAX_BASE
ENDIF
RESTART

@SELECT_TARGET
;表示させるキャラを抽出（LOCAL:2に人数）
VARSET LOCAL, 0
CALLF CLEAR_LIST
LOCAL:1 = CHARANUM / 20

$PRINT_LIST
DRAWLINE
SIF TARGET >= 0
	PRINTFORML 现在选择角色是:%CALLNAME:TARGET%
PRINTFORML 你要选择哪个对象？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT CHARANUM
	SIF COUNT >= LOCAL * 20 && COUNT < (LOCAL+1) * 20
		PRINTFORML [{COUNT}]%NAME:COUNT%
REND
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前页\@
PRINTLC [1000]返回
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [1009]次页\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

TARGET = RESULT
PRINTFORML %CALLNAME:TARGET% 已选择

;--------------------------------------------------
; PALAM操作
;--------------------------------------------------
@EDIT_PALAM
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	PRINTFORML [{LOCAL, 3}]%PALAMNAME:LOCAL% ({PALAM:LOCAL}) 
NEXT
PRINTLC [199]PALAM 重置
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 199
	VARSET PALAM, 0
	PRINTW PALAM已全部重置
	RESTART
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		GOTO INPUT_LOOP
	DRAWLINE
	PRINTFORML %PALAMNAME:LOCAL%修改为：(现在{PALAM:LOCAL})
	PRINTFORML 请输入数值[0-9999999]（无效数值则取消）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	PALAM:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; BASE操作
;--------------------------------------------------
@EDIT_BASE
Z = 0
DRAWLINE
FOR LOCAL, 0, 100
	PRINTFORMLC [{LOCAL, 3}] \@ (STRLENS(BASENAME:LOCAL) == 0) ? BASE:{LOCAL, 3} # %BASENAME:LOCAL%\@ = {BASE:LOCAL}
    CALL CHECK_NEWLINEDEBUG
NEXT
PRINTLC [199]BASE重置
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 199
	VARSET BASE, 0
	PRINTW BASE已全部重置
	RESTART
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	SIF STRLENS(BASENAME:LOCAL) == 0
		GOTO INPUT_LOOP
	DRAWLINE
	PRINTFORML \@ (STRLENS(BASENAME:LOCAL) == 0) ? BASE:{LOCAL} # %BASENAME:LOCAL%\@ 修改为：(现在{BASE:LOCAL})
;	PRINTFORML %BASENAME:LOCAL%的变更(现在{BASE:LOCAL})
	PRINTFORML 请输入数值[0-9999999]（无效数值则取消）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	BASE:LOCAL = RESULT
ENDIF
RESTART


;--------------------------------------------------
; ABL操作
;--------------------------------------------------
;ABL変更
@EDIT_ABL
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(ABLNAME:LOCAL) == 0
		CONTINUE
	PRINTFORML [{LOCAL, 3}]%ABLNAME:LOCAL%LV ({ABL:LOCAL})
NEXT
PRINTL
PRINTLC [200]返回
PRINTLC [201]全ABL修改为99
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(ABLNAME:LOCAL) == 0
		CONTINUE
	ABL:LOCAL = 99
	NEXT
	PRINTW ABL 全部修改为99了
	RESTART
SIF RESULT < 0 || RESULT > 107
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML %ABLNAME:LOCAL%LV修改为(现在{ABL:LOCAL})
	PRINTFORML 请输入数值[-999-999]（无效数值则取消）
	INPUT
	SIF RESULT < -999|| RESULT > 999
		RESTART
	ABL:LOCAL = RESULT
ENDIF

RESTART

;--------------------------------------------------
;素質編集
;--------------------------------------------------
@EDIT_TALENT
LOCAL = 0
LOCAL:1 = 5
CALLF CLEAR_LIST
;表示させる素質を抽出
REPEAT 1000
	;存在しない素質は排除
	STRLENS TALENTNAME:COUNT
	SIF RESULT == 0
		CONTINUE
	CALLF SET_LIST, COUNT, 1
REND
$PRINT_LIST
DRAWLINE
PRINTFORML 要修改哪个素质呢？ ＜page.{LOCAL+1}＞
DRAWLINE
E = 0
REPEAT 60
	LOCAL:2 = COUNT + LOCAL * 60
	SIF GET_LIST(LOCAL:2) == 0
		CONTINUE
	IF TALENT:(LOCAL:2) == 0
		PRINTFORMLC [{LOCAL:2, 3}]×【%TALENTNAME:(LOCAL:2)%】
	ELSEIF TALENT:(LOCAL:2) == 1
		PRINTFORMLC [{LOCAL:2, 3}]◎【%TALENTNAME:(LOCAL:2)%】
	ELSEIF TALENT:(LOCAL:2) > 1
		PRINTFORMLC [{LOCAL:2, 3}]○【%TALENTNAME:(LOCAL:2)%】
	ENDIF
	E += 1
    IF E % 4 == 0
    	PRINTL
    ENDIF
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前页\@
PRINTLC [9999]返回
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [1009]次页\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 9999
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ELSEIF GET_LIST(RESULT) == 0
	GOTO INPUT_LOOP
ENDIF
SIF STRLENS(TALENTNAME:RESULT) == 0
	GOTO INPUT_LOOP
PRINTLC [9999]返回
LOCAL:1 = RESULT
PRINTL
DRAWLINE
PRINTFORML %TALENTNAME:(RESULT)%修改为
PRINTFORML 请输入数值（-999 - 999, 请参照CSV文件进行）
INPUT
SIF RESULT < -999 || RESULT > 999
	RESTART
TALENT:(LOCAL:1) = RESULT
GOTO PRINT_LIST


;--------------------------------------------------
; MARK操作
;--------------------------------------------------
;MARK変更
@EDIT_MARK
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(MARKNAME:LOCAL) == 0
		CONTINUE
	PRINTFORML [{LOCAL, 3}]%MARKNAME:LOCAL% ({MARK:LOCAL})
NEXT
PRINTLC [100]返回
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ENDIF
LOCAL = RESULT
SIF STRLENS(MARKNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %MARKNAME:LOCAL%LV修改为(现在{MARK:LOCAL})
PRINTFORML 请输入数值[0-3]（无效数值则取消）
INPUT
SIF RESULT < 0 || RESULT >3
	RESTART
MARK:LOCAL = RESULT

RESTART

;--------------------------------------------------
; EXP操作
;--------------------------------------------------
;EXP変更
@EDIT_EXP
Z = 0
DRAWLINE
PRINTFORML 要修改哪个经验呢？ ＜page.{LOCAL+1}＞
DRAWLINE
FOR LOCAL:1, 0, 100
	LOCAL = LOCAL:1
	SIF STRLENS(EXPNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%EXPNAME:LOCAL% ({EXP:LOCAL})　
    CALL CHECK_NEWLINEDEBUG
NEXT
PRINTL
DRAWLINE
PRINTLC [9999]返回
PRINTLC [9998]EXP全部为999
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 9999
	RETURN 0
ELSEIF RESULT == 9998
	FOR LOCAL, 0, 100
	SIF STRLENS(EXPNAME:LOCAL) == 0
		CONTINUE
	EXP:LOCAL = 999
	NEXT
	PRINTW EXP 全部最大化处理
	RESTART
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ELSE
LOCAL = RESULT
SIF STRLENS(EXPNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %EXPNAME:LOCAL%修改为(现在{EXP:LOCAL})
PRINTFORML 请输入数值（负数则取消）
INPUT
SIF RESULT < 0
	RESTART
EXP:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; JUEL操作
;--------------------------------------------------
;JUEL変更
@EDIT_JUEL
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	PRINTFORML [{LOCAL, 3}]%PALAMNAME:LOCAL%之珠({JUEL:LOCAL})
NEXT
PRINTLC [200]返回
PRINTLC [201]JUEL全部9999999
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	JUEL:LOCAL = 9999999
	NEXT
	PRINTW JUEL最大化处理完毕
	RESTART
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ELSE
LOCAL = RESULT
SIF STRLENS(PALAMNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %PALAMNAME:LOCAL%之珠修改为：(现在{JUEL:LOCAL})
PRINTFORML 请输入数值（负数则取消）
INPUT
SIF RESULT < 0
	RESTART
JUEL:LOCAL = RESULT
ENDIF
RESTART


;--------------------------------------------------
; CFLAG操作
;--------------------------------------------------
@EDIT_CFLAG
Z = 0
LOCAL = 0
$PRINT_LIST
DRAWLINE
PRINTFORML 现在目前的CFLAG (MAX = 1000)
PRINTFORML 哪些需要修改？ ;＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 1000
	SIF STRLENS(CFLAGNAME:COUNT) == 0
		CONTINUE
	PRINTFORMLC [{COUNT, 3}] \@ (STRLENS(CFLAGNAME:COUNT) == 0) ? CFLAG:{COUNT, 3} # %CFLAGNAME:COUNT%\@ = {CFLAG:COUNT}
    CALL CHECK_NEWLINEDEBUG
REND
PRINTL 
DRAWLINE
PRINTLC [1000]返回
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0

ELSEIF RESULT < 0 || RESULT > 10000
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML \@ (STRLENS(CFLAGNAME:RESULT) == 0) ? CFLAG:{RESULT} # %CFLAGNAME:RESULT%\@ 修改为(现在{CFLAG:RESULT})
PRINTFORML 请输入数值（数值-999则取消）
INPUT
SIF RESULT == -999
	RESTART
CFLAG:(LOCAL:1) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;TFLAG編集
;--------------------------------------------------
@EDIT_TFLAG
Z = 0
LOCAL = 0
$PRINT_LIST
DRAWLINE
PRINTFORML 现在目前的TFLAG
PRINTFORML 哪些需要修改？; ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 1000
	SIF STRLENS(TFLAGNAME:COUNT) == 0
		CONTINUE
	PRINTFORMLC [{COUNT, 3}] \@ (STRLENS(TFLAGNAME:COUNT) == 0) ? TFLAG:{COUNT, 3} # %TFLAGNAME:COUNT%\@ = {TFLAG:COUNT}
    CALL CHECK_NEWLINEDEBUG
REND
PRINTL 
DRAWLINE
PRINTLC [1000]返回
PRINTLC [1010]调教FLAG全部重置(要注意)
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1010
	VARSET TFLAG, 0
	PRINTW 调教FLAG已全部重置
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML \@ (STRLENS(TFLAGNAME:RESULT) == 0) ? TFLAG:{RESULT} # %TFLAGNAME:RESULT%\@ 修改为(现在{TFLAG:RESULT})
PRINTFORML 请输入数值（负数为取消）
INPUT
SIF RESULT < 0
	RESTART
TFLAG:(LOCAL:1) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;FLAG編集
;--------------------------------------------------
;説明付与
@DEFINE_FLAG, ARG
IF STRLENS(FLAGNAME:ARG) > 0
	PRINTS FLAGNAME:ARG
ELSE
	PRINT ――
ENDIF

@EDIT_FLAG
Z = 0
VARSET LOCAL, 0
LOCAL:1 = 199
LOCAL:3 = 0
$PRINT_LIST
IF LOCAL:3
	DRAWLINE
	PRINTFORML 现在目前的FLAG
	PRINTFORML 哪些需要修改？
	DRAWLINE
	REPEAT 10000
		SIF STRLENS(FLAGNAME:COUNT) == 0
			CONTINUE
		PRINTFORMLC [{COUNT, 4}] \@ (STRLENS(FLAGNAME:COUNT) == 0) ? FLAG:{COUNT, 4} # %FLAGNAME:COUNT%\@ = {FLAG:COUNT}
        CALL CHECK_NEWLINEDEBUG
	REND
	PRINTL 
	DRAWLINE
	PRINTLC [10000]返回
	PRINTL
	PRINTLC [10020]切换显示（全部/有效）
	PRINTL 
ELSE
	DRAWLINE
	PRINTFORML 现在目前的FLAG
	PRINTFORML 哪些需要修改？ ＜page.{LOCAL+1}＞
	DRAWLINE
	REPEAT 10000
		SIF COUNT < LOCAL * 60 || COUNT >= (LOCAL + 1) * 60
			CONTINUE
		PRINTFORMLC [{COUNT, 4}] \@ (STRLENS(FLAGNAME:COUNT) == 0) ? FLAG:{COUNT, 4} # %FLAGNAME:COUNT%\@ = {FLAG:COUNT}
		;PRINTFORML [{COUNT, 4}] FLAG:{COUNT, 4} = {FLAG:COUNT}
        CALL CHECK_NEWLINEDEBUG
		
	REND
	PRINTL 
	DRAWLINE
	PRINTFORMLC \@(LOCAL <= 0) ? %" " * 17% # [10001]前页\@
	PRINTLC [10000]返回
	PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 17% # [10009]次页\@
	PRINTL 
	PRINTFORMLC \@(LOCAL/10 <= 0) ? %" " * 17% # [10011]10页以前\@
	PRINTFORMLC %" " * 17%
	PRINTFORMLC \@(LOCAL/10 >= LOCAL:1/10) ? %" " * 17% # [10019]10页以后\@
	PRINTL
	PRINTL [10020]切换显示（全部/有效）
	PRINTL 

ENDIF

$INPUT_LOOP
INPUT
IF RESULT == 10000
	RETURN 0
ELSEIF RESULT == 10001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10011 && LOCAL/10 > 0
	LOCAL -= 10
	GOTO PRINT_LIST
ELSEIF RESULT == 10019 && LOCAL/10 < LOCAL:1/10
	LOCAL += 10
	GOTO PRINT_LIST
ELSEIF RESULT == 10020
	IF LOCAL:3
		LOCAL:3=0
	ELSE
		LOCAL:3=1
	ENDIF
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 10000
	GOTO INPUT_LOOP

ENDIF
LOCAL:2 = RESULT
DRAWLINE
PRINTFORM FLAG:{LOCAL:2}(
CALL DEFINE_FLAG, LOCAL:2
PRINTFORML )修改为(现在{FLAG:(LOCAL:2)})
PRINTFORML 请输入数值（负数则取消）
INPUT
SIF RESULT < 0
	RESTART
FLAG:(LOCAL:2) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
; MAXBASE操作
;--------------------------------------------------
@EDIT_MAX_BASE
Z = 0
DRAWLINE
FOR LOCAL, 0, 100
	PRINTFORMLC [{LOCAL, 3}] \@ (STRLENS(BASENAME:LOCAL) == 0) ? MAXBASE:{LOCAL, 3} # %BASENAME:LOCAL%\@ = {MAXBASE:LOCAL}
    CALL CHECK_NEWLINEDEBUG
NEXT
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORMLCD \@ (STRLENS(BASENAME:LOCAL) == 0) ? MAXBASE:{LOCAL} # %BASENAME:LOCAL%\@ の上限修改(现在{MAXBASE:LOCAL})
	PRINTFORML 请输入数值[0-9999999]（无效数值则取消）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	MAXBASE:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; ITEM操作
;--------------------------------------------------
;ITEMの有無をCSVから取得
@DISP_ITEM(ARG)
#FUNCTION
RETURNF (STRLENS(ITEMNAME:ARG)) ? 1 # 0

;ITEM変更
@EDIT_ITEM
DRAWLINE
FOR LOCAL:1, 0, 1000
	LOCAL = LOCAL:1
	SIF DISP_ITEM(LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%ITEMNAME:LOCAL% ({ITEM:LOCAL})　
	SIF LOCAL:1 % 3 == 0
		PRINTL
NEXT
PRINTLC [9999]返回
PRINTL 
$INPUT_LOOP
INPUT
SIF RESULT == 9999
	RETURN 0
SIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
LOCAL = RESULT
SIF STRLENS(ITEMNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %ITEMNAME:LOCAL%修改为(现在{ITEM:LOCAL})
PRINTFORML 请输入数值（负数则取消）
INPUT
SIF RESULT < 0
	RESTART
ITEM:LOCAL = RESULT
RESTART

;--------------------------------------------------
;NEXTDAY
;--------------------------------------------------
@NEXTDAY
DAY += 1
BEGIN AFTERTRAIN

@CHECK_NEWLINEDEBUG
Z += 1
IF Z == 3
	PRINTL
	Z = 0
ENDIF