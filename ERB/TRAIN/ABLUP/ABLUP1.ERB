;欲望の可否
@ISABLUP1
;恋慕、相亲相爱、淫乱、娼妇はMAXLV10
IF ABL:1 >= 10
	RETURN 0
ELSEIF ABL:1 >= 5 && TALENT:3 == 0 && TALENT:6 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP1

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF

;欲望
@ABLUP1
DRAWLINE
;恋慕、相亲相爱、淫乱、娼妇はMAXLV10
IF ABL:1 >= 10
	PRINTW 已达MAX
	RETURN 0
ELSEIF ABL:1 >= 5 && TALENT:3 == 0 && TALENT:6 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	PRINTW 已达MAX
	RETURN 0
ENDIF

CALL DECIDE_ABLUP1

;ＬＶ４から５に上げるときは异常经验必要（素質：[接受快感][淫乱][娼妇]なら無視できる）
IF ABL:1 == 4 && TALENT:70 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	PRINTS EXPNAME:50
	PRINTL 取得后才能继续升级
;ＬＶ７から８に上げるときは异常经验3以上必要（素質：[接受快感][淫乱][娼妇]なら無視できる）
ELSEIF ABL:1 == 7 && TALENT:70 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	PRINTS EXPNAME:50
	PRINTL 3以上才能继续升级
ENDIF

PRINT [0] - 
PRINTS PALAMNAME:6
PRINT 之珠×
PRINTV A
PRINT ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠子不足 
	SIF I & 2
		PRINT 经验不足 
ENDIF
PRINTL 

PRINTL [100] - 放弃


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 尚未满足条件
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:1 += 1

IF RESULT == 0
	JUEL:6 -= A
ENDIF

PRINTS ABLNAME:1
PRINT 的等级升到了
PRINTV ABL:1
PRINTW ！

CALL YOKUBO_UP_CHECK

;欲望LvUP判定
@DECIDE_ABLUP1
;条件別にＯＫかダメかを記録する
I = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:1 == 0
	A = 1
ELSEIF ABL:1 == 1
	A = 20
ELSEIF ABL:1 == 2
	A = 1000
ELSEIF ABL:1 == 3
	A = 8000
ELSEIF ABL:1 == 4
	A = 24000
ELSEIF ABL:1 == 5
	A = 30000
ELSEIF ABL:1 == 6
	A = 36000
ELSEIF ABL:1 == 7
	A = 45000
ELSEIF ABL:1 == 8
	A = 55000
ELSEIF ABL:1 == 9
	A = 70000
ENDIF

;难以越过的底线
IF TALENT:27 && (TALENT:3 == 0 && TALENT:4 == 0 && TALENT:5 == 0 && TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0)
	TIMES A , 2.00
ENDIF

;ＬＶ４から５に上げるときは异常经验必要（素質：[接受快感][淫乱][娼妇]なら無視できる）
IF ABL:1 == 4 && TALENT:70 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	IF EXP:50 == 0
		;异常经验がない
		I |= 2
	ENDIF
;ＬＶ７から８に上げるときは异常经验3以上必要（素質：[接受快感][淫乱][娼妇]なら無視できる）
ELSEIF ABL:1 == 7 && TALENT:70 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	IF EXP:50 < 3
		;异常经验が3未満
		I |= 2
	ENDIF
ENDIF

;0になった場合は1にする
SIF A == 0
	A = 1

;情欲之珠で上げる
SIF JUEL:6 < A
	I |= 1

IF I == 0
	Z = 1
ENDIF
RETURN 1

