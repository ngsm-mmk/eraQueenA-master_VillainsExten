;同性倾向の可否
@ISABLUP20
;恋慕、相亲相爱、淫乱、娼妇はMAXLV10
IF ABL:20 >= 10
	RETURN 0
ELSEIF ABL:20 >= 5 && TALENT:3 == 0 && TALENT:6 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP20

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;同性倾向
@ABLUP20
DRAWLINE
;恋慕、相亲相爱、淫乱、娼妇はMAXLV10
IF ABL:20 >= 10
	PRINTW 已达MAX
	RETURN 0
ELSEIF ABL:20 >= 5 && TALENT:3 == 0 && TALENT:6 == 0 && TALENT:4 == 0 && TALENT:7 == 0
	PRINTW 已达MAX
	RETURN 0
ENDIF

CALL DECIDE_ABLUP20

;ＬＶ３から４に上げるときは异常经验必要（素質：[男女通吃]なら無視できる）
IF ABL:20 == 3 && TALENT:81 == 0
	PRINTS EXPNAME:50
	PRINTL 取得后才能继续升级
;ＬＶ４から５に上げるときは异常经验3以上必要（素質：[男女通吃]なら無視できる）
ELSEIF ABL:20 == 4 && TALENT:81 == 0
	PRINTS EXPNAME:50
	PRINTL 3以上才能继续升级
;ＬＶ８から９に上げるときは异常经验5以上必要（素質：[男女通吃]なら無視できる）
ELSEIF ABL:20 == 8 && TALENT:81 == 0
	PRINTS EXPNAME:50
	PRINTL 5以上才能继续升级
ENDIF

PRINT [0] - 
PRINTS PALAMNAME:6
PRINT 之珠×
PRINTV A
IF C > 0
	PRINT 、
	PRINTS PALAMNAME:7
	PRINT 之珠×
	PRINTV C
ENDIF
PRINT 、
PRINTS EXPNAME:41
PRINTV B
PRINT 以上
PRINT ……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠子不足 
	SIF I & 2
		PRINT 经验不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF D > 0
	PRINT [1] - 
	PRINTS PALAMNAME:0
	PRINT 之珠×
	PRINTV D
	PRINT 、
	PRINTS EXPNAME:41
	PRINTV B
	PRINT 以上
	PRINT ……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠子不足 
		SIF J & 2
			PRINT 经验不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	J = 256
ENDIF

PRINTL [100] - 放弃


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 尚未满足条件
	RESTART
ELSEIF J == 256 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 尚未满足条件
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:20 += 1

IF RESULT == 0
	JUEL:6 -= A
	JUEL:7 -= C
ELSEIF RESULT == 1
	JUEL:0 -= D
ENDIF

PRINTS ABLNAME:20
PRINT 的等级升到了
PRINTV ABL:20
PRINTW ！


;同性倾向のLVUP判定
@DECIDE_ABLUP20
;条件別にＯＫかダメかを記録する
I = 0
J = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:20 == 0
	A = 200
	B = 50
	C = 0
	D = 1000
ELSEIF ABL:20 == 1
	A = 1000
	B = 200
	C = 0
	D = 5000
ELSEIF ABL:20 == 2
	A = 3000
	B = 500
	C = 1000
	D = 0
ELSEIF ABL:20 == 3
	A = 8000
	B = 1000
	C = 2000
	D = 0
ELSEIF ABL:20 == 4
	A = 20000
	B = 2000
	C = 5000
	D = 0
ELSEIF ABL:20 == 5
	A = 25000
	B = 2300
	C = 6000
	D = 0
ELSEIF ABL:20 == 6
	A = 30000
	B = 2600
	C = 8000
	D = 0
ELSEIF ABL:20 == 7
	A = 35000
	B = 3000
	C = 10000
	D = 0
ELSEIF ABL:20 == 8
	A = 40000
	B = 3500
	C = 15000
	D = 0
ELSEIF ABL:20 == 9
	A = 50000
	B = 4000
	C = 20000
	D = 0
ENDIF

;淫乱
SIF TALENT:4
	TIMES A , 0.80
;娼妇
SIF TALENT:7
	TIMES A , 0.50
;难以越过的底线
IF TALENT:27 && (TALENT:3 == 0 && TALENT:4 == 0 && TALENT:5 == 0 && TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0)
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
	TIMES D , 2.00
ENDIF

;ＬＶ３から４に上げるときは异常经验必要（素質：[男女通吃]なら無視できる）
IF ABL:20 == 3 && TALENT:81 == 0
	IF EXP:50 == 0
		;异常经验が不足
		I |= 2
		J |= 2
	ENDIF
;ＬＶ４から５に上げるときは异常经验3以上必要（素質：[男女通吃]なら無視できる）
ELSEIF ABL:20 == 4 && TALENT:81 == 0
	IF EXP:50 < 3
		;异常经验が不足
		I |= 2
		J |= 2
	ENDIF
;ＬＶ８から９に上げるときは异常经验5以上必要（素質：[男女通吃]なら無視できる）
ELSEIF ABL:20 == 8 && TALENT:81 == 0
	IF EXP:50 < 5
		;异常经验が不足
		I |= 2
		J |= 2
	ENDIF
ENDIF

;情欲之珠で上げる
SIF JUEL:6 < A
	I |= 1
;屈服之珠で上げる
SIF JUEL:7 < C
	I |= 1
;蔷薇经验が必要
SIF EXP:41 < B
	I |= 2

IF I == 0
	Z = 1
ENDIF

IF D > 0
	;快Ｃ之珠で上げる
	SIF JUEL:0 < D
		J |= 1
	;蔷薇经验が必要
	SIF EXP:41 < B
		J |= 2
	
	IF J == 0
		Z = 1
	ENDIF
ELSE
	J = 256
ENDIF
RETURN 1

