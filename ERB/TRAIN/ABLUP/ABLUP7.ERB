;侍奉精神の可否
@ISABLUP7
;恋慕、相亲相爱、服从、隶属はMAXLV10
IF ABL:7 >= 10
	RETURN 0
ELSEIF ABL:7 >= 5 && TALENT:3 == 0 && TALENT:6 == 0 && TALENT:5 == 0 && TALENT:8 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP7

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;侍奉精神
@ABLUP7
DRAWLINE
;恋慕、相亲相爱、服从、隶属はMAXLV10
IF ABL:7 >= 10
	PRINTW 已达MAX
	RETURN 0
ELSEIF ABL:7 >= 5 && TALENT:3 == 0 && TALENT:6 == 0 && TALENT:5 == 0 && TALENT:8 == 0
	PRINTW 已达MAX
	RETURN 0
ENDIF

CALL DECIDE_ABLUP7

;ＬＶ４から５に上げるときは异常经验以上必要（素質：[恋慕][相亲相爱]なら無視できる）
IF ABL:7 == 4 && TALENT:3 == 0 && TALENT:6 == 0
	PRINTS EXPNAME:50
	PRINTL 取得后才能继续升级
;ＬＶ８から９に上げるときは异常经验5以上必要（素質：[恋慕][相亲相爱]なら無視できる）
ELSEIF ABL:7 == 8 && TALENT:3 == 0 && TALENT:6 == 0
	PRINTS EXPNAME:50
	PRINTL 5以上才能继续升级
ENDIF

;顺从が必要
PRINTS ABLNAME:0
PRINT LV
PRINTV ABL:7 + 1
PRINTL 以上才能继续升级

PRINT [0] - 
PRINTS PALAMNAME:7
PRINT 之珠×
PRINTV A
IF E > 0
	PRINT 、
	PRINTS EXPNAME:2
	PRINTV E
	PRINT 以上
	PRINT 、
	IF TALENT:MASTER:140
		PRINTS EXPNAME:22
	ELSEIF TALENT:MASTER:137 || TALENT:MASTER:139
		PRINTS EXPNAME:22 + "+" + EXPNAME:25
	ELSE
		PRINTS EXPNAME:25
	ENDIF
	PRINTV E*2
	PRINT 以上
ENDIF
PRINT ……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠子不足 
	SIF I & 2
		PRINT 经验不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF B > 0
	;恭顺之珠で上げる
	SIF JUEL:5 < B
		J |= 1
	;この場合侍奉快乐经验が必要なことあり
	SIF EXP:21 < D
		J |= 2

	PRINT [1] - 
	PRINTS PALAMNAME:5
	PRINT 之珠×
	PRINTV B
	IF D > 0
		PRINT 、
		PRINTS EXPNAME:21
		PRINTV D
		PRINT 以上
	ENDIF
	PRINT ……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠子不足 
		SIF J & 2
			PRINT 经验不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	J = 256
ENDIF

IF C > 0
	;学习之珠で上げる
	SIF JUEL:8 < C
		K |= 1
	;この場合绝顶经验が１以上必要
	SIF EXP:2 < 1
		K |= 2

	PRINT [2] - 
	PRINTS PALAMNAME:8
	PRINT 之珠×
	PRINTV C
	PRINT 、
	PRINTS EXPNAME:2
	PRINTV 1
	PRINT 以上
	PRINT ……

	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT 珠子不足 
		SIF K & 2
			PRINT 经验不足 
		SIF K & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	K = 256
ENDIF

PRINTL [100] - 放弃

$INPUT_LOOP
INPUT
IF (RESULT < 0 || RESULT > 2) && RESULT != 100
	GOTO INPUT_LOOP
ELSEIF I != 0 && RESULT == 0
	PRINTL 尚未满足条件，请再次输入
	GOTO INPUT_LOOP
ELSEIF J == 256 && RESULT == 1
	GOTO INPUT_LOOP
ELSEIF J != 0 && RESULT == 1
	PRINTL 尚未满足条件，请再次输入
	GOTO INPUT_LOOP
ELSEIF K == 256 && RESULT == 2
	GOTO INPUT_LOOP
ELSEIF K != 0 && RESULT == 2
	PRINTL 尚未满足条件，请再次输入
	GOTO INPUT_LOOP
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:7 += 1

IF RESULT == 0
	JUEL:7 -= A
ELSEIF RESULT == 1
	JUEL:5 -= B
ELSEIF RESULT == 2
	JUEL:8 -= C
ENDIF

PRINTS ABLNAME:7
PRINT 的等级升到了
PRINTV ABL:7
PRINTW ！

;侍奉精神LvUP判定
@DECIDE_ABLUP7
;条件別にＯＫかダメかを記録する
I = 0
J = 0
K = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:7 == 0
	A = 100
	B = 20
	C = 100
	D = 5
	E = 5
ELSEIF ABL:7 == 1
	A = 1200
	B = 100
	C = 0
	D = 5
	E = 10
ELSEIF ABL:7 == 2
	A = 5000
	B = 600
	C = 0
	D = 20
	E = 20
ELSEIF ABL:7 == 3
	A = 10000
	B = 2000
	C = 0
	D = 50
	E = 20
ELSEIF ABL:7 == 4
	A = 30000
	B = 8000
	C = 0
	D = 100
	E = 30
ELSEIF ABL:7 == 5
	A = 35000
	B = 8500
	C = 0
	D = 150
	E = 40
ELSEIF ABL:7 == 6
	A = 40000
	B = 10000
	C = 0
	D = 200
	E = 100
ELSEIF ABL:7 == 7
	A = 45000
	B = 15000
	C = 0
	D = 250
	E = 150
ELSEIF ABL:7 == 8
	A = 50000
	B = 20000
	C = 0
	D = 300
	E = 200
ELSEIF ABL:7 == 9
	A = 60000
	B = 30000
	C = 0
	D = 400
	E = 300
ENDIF

;恋慕
SIF TALENT:3
	TIMES B , 0.80
;相亲相爱
SIF TALENT:6
	TIMES B , 0.60
;服从
SIF TALENT:5
	TIMES A , 0.70
;隶属
SIF TALENT:8
	TIMES A , 0.40
;难以越过的底线
IF TALENT:27 && (TALENT:3 == 0 && TALENT:4 == 0 && TALENT:5 == 0 && TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0)
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
	TIMES D , 2.00
	TIMES E , 2.00
ENDIF

;顺从が侍奉精神＋１レベルでないといけない
IF ABL:0 < ABL:7+1
	;顺从が不足
	I |= 4
	J |= 4
	K |= 4
ENDIF

;ＬＶ４から５に上げるときは异常经验以上必要（素質：[恋慕][相亲相爱]なら無視できる）
IF ABL:7 == 4 && TALENT:3 == 0 && TALENT:6 == 0
	IF EXP:50 == 0
		;异常经验が不足
		I |= 2
		J |= 2
		K |= 2
	ENDIF
;ＬＶ８から９に上げるときは异常经验5以上必要（素質：[恋慕][相亲相爱]なら無視できる）
ELSEIF ABL:7 == 8 && TALENT:3 == 0 && TALENT:6 == 0
	IF EXP:50 < 5
		;异常经验が不足
		I |= 2
		J |= 2
		K |= 2
	ENDIF
ENDIF

;屈服之珠で上げる
SIF JUEL:7 < A
	I |= 1
;绝顶经验が必要な場合
SIF EXP:2 < E
	I |= 2
;舔阴與口交经验が必要な場合
SIF (EXP:22 + EXP:25) < E*2
	I |= 2

IF I == 0
	Z = 1
ENDIF

IF B > 0
	;恭顺之珠で上げる
	SIF JUEL:5 < B
		J |= 1
	;この場合侍奉快乐经验が必要なことあり
	SIF EXP:21 < D
		J |= 2
	
	IF J == 0
		Z = 1
	ENDIF
ELSE
	J = 256
ENDIF

IF C > 0
	;学习之珠で上げる
	SIF JUEL:8 < C
		K |= 1
	;この場合绝顶经验が１以上必要
	SIF EXP:2 < 1
		K |= 2

	IF K == 0
		Z = 1
	ENDIF
ELSE
	K = 256
ENDIF
RETURN 1

