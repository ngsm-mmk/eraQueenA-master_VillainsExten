
;============================================================================== 
;◆指令過濾
;============================================================================== 
@CONFIG_COM_FILTER
	;頁数の計算
	LOCAL:20 = 0
	FOR LOCAL:0, 0, 1000
		IF TRAINNAME:(LOCAL:0) != ""
			LOCAL:20 ++
		ENDIF
	NEXT
	LOCAL:20 = (LOCAL:20 - 1) / 66 + 1
	LOCAL:21 = 1

	$SHOW_LOOP
	CLEARLINE LINECOUNT
	DRAWLINE
	SETCOLOR 0x787878
	PRINTFORM 呈现灰色的指令：
	RESETCOLOR
	PRINTFORML 将不会在调教画面中出现
	PRINTFORM 若有重复的指令：
	SETCOLOR 0xFFBFBF
	PRINTFORML 粉色指令为女主人-->男奴隶用
	RESETCOLOR
	DRAWLINE
	;命令の表示
	LOCAL:22 = 0
	LOCAL:23 = (LOCAL:21 - 1) * 66
	LOCAL:24 = LOCAL:21 * 66
	LOCAL:25 = 0
	LOCAL:26 = 6
	FOR LOCAL:0, 0, 1000
		SIF  LOCAL:0 == 0
			CONTINUE
		SIF  LOCAL:0 == 400 || LOCAL:0 == 600
			CONTINUE
		IF TRAINNAME:(LOCAL:0) != ""
			IF LOCAL:22 >= LOCAL:23 && LOCAL:22 < LOCAL:24
				IF LOCAL:25 >= 1 && LOCAL:25 % 3 == 0
					PRINTL 
					LOCAL:26 ++
				ENDIF
				IF ( LOCAL:0 >= 52 &&  LOCAL:0 <= 58) ||  LOCAL:0 == 79 ||  LOCAL:0 == 99 || ( LOCAL:0 >= 216 &&  LOCAL:0 <= 219) 
					SETCOLOR 0xFFBFBF
				ELSE
					SETCOLOR 0xE8E8E8
				ENDIF
				RESULT = 0
				TRYCALLFORM COM_FILTER_FORCE_OFF{LOCAL:0}
				IF RESULT
					PRINTFORM [{LOCAL:0, 3}] %TRAINNAME:(LOCAL:0), 20, LEFT%				
				ELSE
					SIF COM_FILTER:(LOCAL:0)
						SETCOLOR 0x787878
					PRINTFORM [{LOCAL:0, 3}] %TRAINNAME:(LOCAL:0), 20, LEFT%
				ENDIF
				RESETCOLOR
				LOCAL:25 ++
			ENDIF
			LOCAL:22 ++
		ENDIF
	NEXT
	PRINTL 
	DRAWLINE
	PRINT   [ 990]上一頁     
	PRINTPLAINFORM page{LOCAL:21}/{LOCAL:20}    
	PRINTL  [ 991]下一頁


	DRAWLINE
	PRINT   [ 999]決定
	
	$INPUT_LOOP
	INPUT 999

	IF RESULT == 999
		;REDRAW 1
		RETURN
	ELSEIF RESULT == 990
		LOCAL:21 --
		IF LOCAL:21 < 1
			LOCAL:21 = LOCAL:20
		ENDIF
		CLEARLINE LOCAL:26
		GOTO SHOW_LOOP
	ELSEIF RESULT == 991
		LOCAL:21 ++
		IF LOCAL:21 > LOCAL:20
			LOCAL:21 = 1
		ENDIF
		CLEARLINE LOCAL:26
		GOTO SHOW_LOOP
	ELSEIF RESULT >= 0 && RESULT < 1000
		LOCAL:5 = RESULT
		IF TRAINNAME:(LOCAL:5) != ""
			RESULT = 0
			TRYCALLFORM COM_FILTER_FORCE_OFF{LOCAL:5}
			IF !RESULT
				COM_FILTER:(LOCAL:5) = !COM_FILTER:(LOCAL:5)
				CLEARLINE LOCAL:26
				GOTO SHOW_LOOP
			ENDIF
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP
	