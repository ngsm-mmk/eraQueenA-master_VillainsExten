@LUNCH_SALE
#DIMS 餐点
;弁当を作らせて売る、もしくは自己食べるオプション。
;定食屋台より儲けは少なめ。イチャイチャ二人でご飯食べさせたい人向け
;もしくは毒を盛られてみたい人向け

;-------------------------------------------------
;K = 料理の種類
;L = 料理の値段の補正を決定
;M = 料理の値段
;N = 料理の値段の基礎
;O = 学习之珠増加量
;P = 固有料理の種類、乱数処理用
;-------------------------------------------------

A = 0
B = TARGET
K = RAND:16
L = 0
M = 0
N = 0


;奴隶の料理技能と家务经验のレベルによって、料理の内容と値段の基礎値が上昇
;ABL:技巧をみる
IF ABL:B:2 == 0
	L = 10
ELSEIF ABL:B:2 == 1
	L = 15
ELSEIF ABL:B:2 == 2
	L = 20
ELSEIF ABL:B:2 == 3
	L = 30
ELSEIF ABL:B:2 == 4
	L = 40
ELSE
	L = 50
ENDIF

;ABL:料理技能をみる
IF ABL:B:12 == 0
	TIMES L , 1.00
ELSEIF ABL:B:12 == 1
	TIMES L , 1.10
ELSEIF ABL:B:12 == 2
	TIMES L , 1.30
ELSEIF ABL:B:12 == 3
	TIMES L , 1.50
ELSEIF ABL:B:12 == 4
	TIMES L , 1.90
ELSE
	TIMES L , 2.40
ENDIF

;EXP:調理经验をみる
IF EXP:B:61 < EXPLV:1
	TIMES L , 0.80
ELSEIF EXP:B:61 < EXPLV:2
	TIMES L , 1.00
ELSEIF EXP:B:61 < EXPLV:3
	TIMES L , 1.20
ELSEIF EXP:B:61 < EXPLV:4
	TIMES L , 1.40
ELSEIF EXP:B:61 < EXPLV:5
	TIMES L , 1.70
ELSE
	TIMES L , 2.00
ENDIF

;自我奉献
SIF TALENT:B:63
	TIMES L , 1.20

;恋慕
SIF TALENT:B:3
	TIMES L , 2.00

;相亲相爱
SIF TALENT:B:6
	TIMES L , 3.00


;K = 料理の種類
IF K == 0 && ABL:B:12 >= 3
	;キャラ固有料理
	N = 500
ELSEIF K == 1 || K == 15
	;肉野菜煮物
	N = 120
ELSEIF K == 2 || K == 14
	;名物定食
	N = 250
ELSEIF K == 3 || K == 13
	;点心
	N = 190
ELSEIF K == 4 || K == 12
	;パン
	N = 150
ELSEIF K == 5 || K == 11
	;サラダ
	N = 90
ELSEIF K == 6 || K == 10
	;お弁当
	N = 200
ELSE
	;スープ
	N = 120
ENDIF


SIF TALENT:B:擅长料理
	TIMES N , 1.50
SIF TALENT:B:不擅料理
	TIMES N , 0.75

IF CFLAG:B:2404 == 1
    PRINTL
    PRINTFORML 房间里的%CALLNAME:B%被叫出来了
    PRINTW
    CFLAG:B:2404 = 0
ELSEIF CFLAG:B:2414 == 1
    C = 0
    REPEAT CHARANUM
        SIF CFLAG:COUNT:(NO:B+3000) == 1
            C = COUNT
    REND
    PRINTL
    PRINTFORML 在%CALLNAME:C%房间里的%CALLNAME:B%被叫出来了
    PRINTW
    CFLAG:B:2414 = 0
    CFLAG:C:(NO:B+3000) = 0
    CFLAG:C:2404 = 0
ENDIF


PRINTL 
;常识改变
SIF CFLAG:B:7022
PRINTFORML 赤身裸体仅穿一条雪白围裙的%CALLNAME:B%，在厨房忙碌着
PRINTFORMW %CALLNAME:B%制作了料理
PRINTL 


IF (TALENT:B:3 || TALENT:B:6)
	PRINTL   「♪～」
	PRINTL  「♪♪～」
	PRINTL 「♪～♪～」
	PRINTL      !
ELSE
	PRINTL      ･
	PRINTL      ･
	PRINTL      ･
	PRINTL      !
ENDIF
PRINTW 
PRINTFORM %CALLNAME:B%把
SIF TALENT:B:6
	PRINTFORM 倾注了满满爱情的
IF ITEM:41 > 0 && (TALENT:B:4 || TALENT:B:7) && RAND:4 == 0
	ITEM:41 -= 1
	TFLAG:14 = 1
	PRINT 飘散着香味的
ENDIF
IF ABL:B:12 >= 5 && RAND:5 > 0
	CALL LUNCH_NAME
ELSEIF K == 1 || K == 15
	餐点 = 炖菜
ELSEIF K == 2 || K == 14
	餐点 = 奶油焗饭
ELSEIF K == 3 || K == 13
	餐点 = 白酱义大利面
ELSEIF K == 4 || K == 12
	餐点 = 面包
ELSEIF K == 5 || K == 11
	餐点 = 沙拉
ELSEIF K == 6 || K == 10
	餐点 = 便当
ELSE
	餐点 = 农汤
ENDIF
PRINTFORM %餐点%
PRINTW 端了上来。
PRINTL      ･
PRINTL      ･
PRINTL      ･
PRINTW 

IF (TALENT:B:3 == 0 || TALENT:B:6 == 0) && (MARK:B:3 >= 2 || ABL:B:0 < 5)

	K = 1
	;常识改变
	IF CFLAG:B:7024
		PRINTFORML %CALLNAME:B%在品嚐自己做的%餐点%味道时，里面的酱汁让他联想到%PLAYER_NAME()%的味道……
		IF !KANRAKU(B)
		PRINTFORMW 一边皱着眉头一边露出一言难尽的表情，一言不发地进食着……
		ELSE
			PRINTFORMW 脸上不由得露出了可疑的晕红，像是要好好品嚐那样慢慢地进食着……
		ENDIF
		JUEL:B:6 += 50
		PRINTFORMW %PALAMNAME:6%之珠＋50
	ELSE
		;口上はEVENT_Mで管理した方がよさそうな気がしたので
		TFLAG:13 = 26
		CALL KOJO_MESSAGE_EVENT
	ENDIF
	PRINTL      ･
	PRINTL      ･
	PRINTL      ･
	BASE:B:0 += L * 6
	JUEL:B:5 += L * 3
	PRINTFORML %CALLNAME:B%的体力回复了。
	PRINTFORMW %PALAMNAME:5%之珠＋{L*3}
ELSE
	PRINTL 好吧……毕竟是难得的作品。
	PRINTW 是卖掉赚点零钱，还是两个人一起享用呢……
	PRINTL （要怎么做？）
	DRAWLINE
	IF TALENT:B:6
		PRINTL [0] - 两人一起享用
		PRINTL [1] - 卖掉料理
	ELSE
		PRINTL [0] - 机会难得，一起享用吧
		PRINTL [1] - 卖掉料理
	ENDIF
	DRAWLINE
	PRINTL 
	$INPUT_LOOP
	INPUT
	IF RESULT == 0
		K = 2
		TFLAG:13 = 26
		;常识改变
		IF CFLAG:B:7024
			PRINTFORML %CALLNAME:B%在品嚐自己做的%餐点%味道时，里面的酱汁让他联想到%PLAYER_NAME()%的味道……
			IF !KANRAKU(B)
				PRINTFORMW 一边皱着眉头一边露出一言难尽的表情，一言不发地进食着……
			ELSE
				PRINTFORMW 脸上不由得露出了可疑的晕红，像是要好好品嚐那样慢慢地进食着……
			ENDIF
			JUEL:B:6 += 50
			PRINTFORMW %PALAMNAME:6%之珠＋50
		ELSE
				CALL KOJO_MESSAGE_EVENT
		ENDIF
		PRINTL      ･
		PRINTL      ･
		PRINTL      ･
		BASE:B:0 += L * 5
		JUEL:B:5 += L * 2
		PRINTFORML %CALLNAME:B%的体力回复了。
		PRINTFORMW %PALAMNAME:5%之珠＋{L*2}
	ELSEIF RESULT == 1
		M = (L * N) / 50
		IF M > 5000
			M = (M - 5000) / 2 + 5000
			IF M > 10000
				M = (M - 10000) / 4 + 10000
				IF M > 50000
					M = (M - 50000) / 8 + 50000
				ENDIF
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:B%
		IF (TALENT:B:3 || TALENT:B:6)
			PRINTFORM 倾注了满满爱情
			TIMES M , 1.50
		ENDIF
		PRINTFORML 的料理以{M}的价格售出了。
		MONEY += M
		PRINTFORMW 金钱增加了 {M}
		TFLAG:14 = 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ENDIF
;体力が上限を超えたら最大値に
SIF BASE:B:0 > MAXBASE:B:0
	BASE:B:0 = MAXBASE:B:0

P = ABL:B:12
JUEL:B:7 += L*(P+1)
PRINTFORML %PALAMNAME:7%之珠＋{L*(P+1)}
O = ABL:B:12 + (1 + RAND:5)
PRINTFORML %EXPNAME:61% ＋{O}
EXP:B:61 += O
;相亲相爱、相爱持ちかつ食後のイベント発生確定時は爱情经验が入る
;また、食後のイベントで推到ことを選ぶと侍奉快乐经验が入るようになる
IF TFLAG:14 == 1 && (TALENT:B:3 || TALENT:B:6)
	PRINTFORML %EXPNAME:23% ＋2
	EXP:B:23 += 2
	TFLAG:14 = 2
;服从持ちのときは食後のイベントで推到ことを選ぶと侍奉快乐经验が入るようになる
ELSEIF TFLAG:14 == 1 && (TALENT:B:5 || TALENT:B:8)
	TFLAG:14 = 2
ENDIF
WAIT

;食後の運動イベント
IF (TFLAG:14 >= 1 && TFLAG:14 <= 2) && ABL:B:1 > 0
	CALL LUNCH_EXERCISE
ELSE
	TFLAG:14 = 0
ENDIF

;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND
RETURN 1


@LUNCH_SELF
#DIMS 餐点
;主人が自己弁当を作って売るオプション。
;-------------------------------------------------
;K = 料理の種類
;L = 料理の値段の補正を決定
;M = 料理の値段
;N = 料理の値段の基礎
;O = 学习之珠増加量
;P = 乱数処理用
;-------------------------------------------------

K = RAND:16
L = 0
M = 0
N = 0
O = 0
P = 0


;ABL:技巧をみる
IF ABL:MASTER:2 == 0
	L = 10
ELSEIF ABL:MASTER:2 == 1
	L = 15
ELSEIF ABL:MASTER:2 == 2
	L = 20
ELSEIF ABL:MASTER:2 == 3
	L = 30
ELSEIF ABL:MASTER:2 == 4
	L = 40
ELSE
	L = 50
ENDIF

;ABL:料理技能をみる
IF ABL:MASTER:12 == 0
	TIMES L , 1.00
ELSEIF ABL:MASTER:12 == 1
	TIMES L , 1.10
ELSEIF ABL:MASTER:12 == 2
	TIMES L , 1.30
ELSEIF ABL:MASTER:12 == 3
	TIMES L , 1.50
ELSEIF ABL:MASTER:12 == 4
	TIMES L , 1.90
ELSE
	TIMES L , 2.40
ENDIF

;EXP:調理经验をみる
IF EXP:MASTER:12 < EXPLV:1
	TIMES L , 0.80
ELSEIF EXP:MASTER:12 < EXPLV:2
	TIMES L , 1.00
ELSEIF EXP:MASTER:12 < EXPLV:3
	TIMES L , 1.20
ELSEIF EXP:MASTER:12 < EXPLV:4
	TIMES L , 1.40
ELSEIF EXP:MASTER:12 < EXPLV:5
	TIMES L , 1.70
ELSE
	TIMES L , 2.00
ENDIF

;自我奉献
SIF TALENT:MASTER:63
	TIMES L , 1.20

;K = 料理の種類
IF K == 0 && ABL:MASTER:12 >= 3
	;得意料理
	N = 500
ELSEIF K == 1 || K == 15
	;肉野菜煮物
	N = 120
ELSEIF K == 2 || K == 14
	;名物定食
	N = 250
ELSEIF K == 3 || K == 13
	;点心
	N = 190
ELSEIF K == 4 || K == 12
	;パン
	N = 150
ELSEIF K == 5 || K == 11
	;サラダ
	N = 90
ELSEIF K == 6 || K == 10
	;お弁当
	N = 200
ELSE
	;スープ
	N = 120
ENDIF


;擅长料理
SIF TALENT:MASTER:擅长料理
	TIMES N , 1.25
;味音痴、不器用、適当
SIF TALENT:MASTER:不擅料理
	TIMES N , 0.75

PRINTL 
PRINTFORMW %PLAYER_NAME()%亲自下厨制作了料理
PRINTL 
PRINTL      ･
PRINTL      ･
PRINTL      ･
PRINTL      !
PRINTW 
IF K == 0 && ABL:MASTER:12 >= 3
	餐点 = 奶油蒜香煎扇贝肉
ELSEIF K == 1 || K == 15
	餐点 = 炖菜
ELSEIF K == 2 || K == 14
	餐点 = 奶油焗饭
ELSEIF K == 3 || K == 13
	餐点 = 白酱义大利面
ELSEIF K == 4 || K == 12
	餐点 = 面包
ELSEIF K == 5 || K == 11
	餐点 = 沙拉
ELSEIF K == 6 || K == 10
	餐点 = 便当
ELSE
	餐点 = 农汤
ENDIF
PRINTFORM %餐点%
PRINTFORMW 被端上了餐桌。
PRINTL      ･
PRINTL      ･
PRINTL      ･
PRINTL 
;ターゲットがいない場合は売るだけ
IF TARGET >= 0
	PRINTL 那么……毕竟是难得的大作。
	PRINTW 是按计划卖掉，还是找个人一起享用呢……
	PRINTL （要怎么做？）
	DRAWLINE
	
	PRINTFORML [0] - 机会难得，和%CALLNAME:TARGET%一起吃吧
	PRINTL [1] - 卖掉料理
	
	DRAWLINE
	PRINTL 
	$INPUT_LOOP
	INPUT
	IF RESULT == 0
		K = 3
		;常识改变
		IF CFLAG:TARGET:7024
			PRINTFORML %CALLNAME:TARGET%在品嚐%PLAYER_NAME()%做的%餐点%味道时，里面的酱汁让他联想到%PLAYER_NAME()%的味道……
			IF !KANRAKU(TARGET)
				PRINTFORMW 一边皱着眉头一边露出一言难尽的表情，一言不发地进食着……
			ELSE
				PRINTFORMW 	脸上不由得露出了可疑的晕红，像是要好好品嚐那样慢慢地进食着……
			ENDIF
			JUEL:TARGET:6 += 50
			PRINTFORMW %PALAMNAME:6%之珠＋50
		ELSE
			;イベント口上呼び出し
			TFLAG:13 = 26
			CALL KOJO_MESSAGE_EVENT
		ENDIF
		PRINTL      ･
		PRINTL      ･
		PRINTL      ･
		BASE:0 += L * 5
		;体力が上限を超えたら最大値に
		SIF BASE:0 > MAXBASE:0
			BASE:0 = MAXBASE:0
		JUEL:5 += L * 4
		PRINTFORML %CALLNAME:TARGET%的体力回复了。
		PRINTFORMW %PALAMNAME:5%之珠＋{L*4}
	ELSEIF RESULT == 1
		M = (L * N) / 50
		IF M > 5000
			M = (M - 5000) / 2 + 5000
			IF M > 10000
				M = (M - 10000) / 4 + 10000
				IF M > 50000
					M = (M - 50000) / 8 + 50000
				ENDIF
			ENDIF
		ENDIF
		PRINTFORML %CALLNAME:MASTER%的料理以{M}的价格售出了。
		MONEY += M
		PRINTFORMW 金钱增加了 {M}
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSE
	M = (L * N) / 50
	IF M > 5000
		M = (M - 5000) / 2 + 5000
		IF M > 10000
			M = (M - 10000) / 4 + 10000
			IF M > 50000
				M = (M - 50000) / 8 + 50000
			ENDIF
		ENDIF
	ENDIF
	PRINTFORML %CALLNAME:MASTER%的料理以{M}的价格售出了。
	MONEY += M
	PRINTFORMW 金钱增加了 {M}
ENDIF
M = 0
O = ABL:MASTER:12 + (1 + RAND:5)
PRINTFORML %EXPNAME:61%＋{O}
EXP:MASTER:61 += O

IF ABL:MASTER:12 == 4 && EXP:MASTER:61 >= 1000
	M = 1
ELSEIF ABL:MASTER:12 == 3 && EXP:MASTER:61 >= 400
	M = 1
ELSEIF ABL:MASTER:12 == 2 && EXP:MASTER:61 >= 100
	M = 1
ELSEIF ABL:MASTER:12 == 1 && EXP:MASTER:61 >= 20
	M = 1
ELSEIF ABL:MASTER:12 == 0 && EXP:MASTER:61 >= 5
	M = 1
ENDIF
IF M == 1
	ABL:MASTER:12 += 1
	PRINTFORMW %CALLNAME:MASTER%的%ABLNAME:12%的等级升到了{ABL:MASTER:12}。
ENDIF

;食後の運動イベント
IF TFLAG:14 == 1 && ABL:1 > 0
	B = TARGET
	CALL LUNCH_EXERCISE
ELSE
	TFLAG:14 = 0
ENDIF

;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND
RETURN 1


@LUNCH_EXERCISE
;SELF.ERBを基に作成。

S = 0
;V感覚
IF ABL:B:4 == 1
	S = 1
ELSEIF ABL:B:4 == 2 || ABL:B:4 == 3
	S = 2
ELSEIF ABL:B:4 >= 4
	S = 3
ENDIF

;A感覚
IF ABL:B:5 == 1
	S = 1
ELSEIF ABL:B:5 == 2 || ABL:B:4 == 3
	S = 2
ELSEIF ABL:B:5 >= 5
	S = 3
ENDIF

;欲望と侍奉精神によるボー茄子
IF ABL:B:1 == 4 && ABL:B:6 >= 4
	S += 2
ELSEIF ABL:B:1 >= 5 && ABL:B:6 >= 4
	S += 3
ENDIF

;欲望ＬＶ３でもSに+1する
IF ABL:B:1 == 3 && ABL:B:4 >= 3 && ABL:B:6 >= 5
	S += 1
ENDIF

;接受快感、否定快感、淫乱化
IF TALENT:B:70 && S
	S += 1
ELSEIF TALENT:B:71 && S
	S -= 2
ENDIF

IF (TALENT:B:4 || TALENT:B:7) && S
	S += 1
ENDIF

;昼の場合は夜までｷｬｯｷｬｳﾌﾌ
IF TIME == 0 && S > 3
	TIME += 1
	S += 2
ENDIF

;反抗刻印があると実行しにくくなる。また、恋慕无反抗3では発生せず
IF MARK:B:3 && S
	IF MARK:B:3 >= 3 && (TALENT:B:3 == 0 || TALENT:B:6 == 0)
		S = 0
	ELSE
		S /= 1+MARK:B:3
		S -= MARK:B:3
	ENDIF
ENDIF

IF S > 0
	DRAWLINE
	PRINTFORML 吃过料理的%PLAYER_NAME(1)%和%CALLNAME:B%被无法形容的感觉袭击了
	PRINTL 
	PRINTL 要怎么做？
	PRINTL [0] - 今天什么也不做
	PRINTFORML [1] - 推倒%CALLNAME:B%
	DRAWLINE
	$SELECT_POINT_EXERCISE
	INPUT
	IF RESULT == 0
		PRINTFORML %PLAYER_NAME(1)%将几乎席卷理智的欲望强行压制住，丢下身后的%CALLNAME:B%一个人回屋去了……
		PRINTL 
		WAIT
		;欲望
		SIF ABL:B:1
			S += ABL:B:1
		;自慰成瘾
		SIF ABL:B:15
			S += ABL:B:10
		;淫乱
		SIF TALENT:B:4 || TALENT:B:7
			S += 1
		;自制力
		SIF TALENT:B:20
			S -= 2
		;快乐の否定
		SIF TALENT:B:71
			S -= 3
		;反抗刻印
		SIF MARK:B:3
			S -= MARK:B:3
		S /= 2
		S -= 3
		SIF TFLAG:14 == 2
			S += 5
		IF S > 0
			PRINTFORM 被留在房间里的%CALLNAME:B%不堪寂寞，情不自禁地自慰了
			PRINTV S
			PRINTL 。
			PRINTFORML %EXPNAME:10%＋{S}
			PRINTFORML %PALAMNAME:0%之珠＋{S*100}
			PRINTFORML %PALAMNAME:5%之珠＋{S*50}
			PRINTFORMW %PALAMNAME:6%之珠＋{S*100}
			EXP:B:10 += S
			JUEL:B:0 += S*100
			JUEL:B:5 += S*50
			JUEL:B:6 += S*100
		ENDIF
	ELSEIF RESULT == 1
		SIF TFLAG:14 == 2
			S += 2
		K = 4
		;イベント口上呼び出し
		TFLAG:13 = 26
		CALL KOJO_MESSAGE_EVENT
		PRINTL 
		WAIT
		TFLAG:130 = 4
		;素敵な食後の運動
		;CALL KOJO_LUNCH_EXERCISE
		PRINTFORML %EXPNAME:4%＋{S}
		EXP:B:4 += S
		;相爱・服从を持っていた場合侍奉快乐经验が入る
		IF TFLAG:14 == 2
			PRINTFORML %EXPNAME:21% ＋2
			EXP:B:21 += 2
		ENDIF
		PRINTFORML %PALAMNAME:5%之珠＋{S*100}
		PRINTFORML %PALAMNAME:6%之珠＋{S*300}
		JUEL:B:5 += S*100
		JUEL:B:6 += S*300
		IF TALENT:B:0
			JUEL:B:5 += S*250
			PRINTFORML %PALAMNAME:5%之珠＋{S*250}
			PRINTL 
			PRINTL 
			IF (TALENT:B:3 || TALENT:B:6) && EXP:B:0 == 0
				PRINTFORML %CALLNAME:B%因向%PLAYER_NAME(1)%献出纯洁而感到无上的幸福……
				CSTR:B:20 = %PLAYER_NAME(1)%
			ELSEIF (TALENT:B:5 || TALENT:B:8) && EXP:B:0 == 0
				PRINTFORML %CALLNAME:B%因向%PLAYER_NAME(1)%献出纯洁而感到极致的快乐……
				CSTR:B:20 = %PLAYER_NAME(1)%
			ELSEIF (TALENT:B:4 || TALENT:B:7) && EXP:B:0 == 0
				PRINTFORML %CALLNAME:B%并未感到破瓜的疼痛，甚至为了引出更多的快乐扭动起腰肢……
				CSTR:B:20 = %PLAYER_NAME(1)%
			ELSEIF TALENT:B:161
				PRINTFORML 从%CALLNAME:B%下身渗出的处子血将床单弄脏了
				CSTR:B:22 = %PLAYER_NAME(1)%
			ELSE
				PRINTFORML 沉迷于性交中的二人忘记了周围的一切……
			ENDIF
			TALENT:B:0 = 0
		ENDIF
		WAIT
		IF ABL:B:0+ABL:B:1+ABL:B:7 >= 13 || CFLAG:B:2 > 750
			PRINTFORML 不久后，不知是谁先抽离了身体，两人相视而笑
			PRINTFORMW 不能一直留在这里，于是两人一起恋恋不舍地离开了房间…
		ENDIF
	ELSE
		GOTO SELECT_POINT_EXERCISE
	ENDIF
ENDIF
TFLAG:14 = 0
S = 0
